# This is a comment.

# Name generation starts with a NAME symbol
# a NAME is usually just a word...

NAME > WORD

# ...but sometimes it will be 2 words, separated by space

NAME > WORD + [space] + WORD * 0.25

# The "* 0.25" above makes the transformation 4 times less likely than without
# that qualifier.

# A word will have anywhere between 1-3 syllables, but let's make
# 2-syllable words most likely, and use "[uppercase]" meta-symbol to make the
# succeeding character uppercase:

WORD > [uppercase] + SYLLABLE
WORD > [uppercase] + SYLLABLE + SYLLABLE * 4
WORD > [uppercase] + SYLLABLE + SYLLABLE + SYLLABLE

# Another meta-symbol is [null] which stands for no characters.

# Our syllable will have a consonant and a vowel:

SYLLABLE > CONSONANT + VOWEL

# Let's define consonants and vowels:

CONSONANT > b * 2
CONSONANT > d * 2
CONSONANT > f
CONSONANT > g * 2
CONSONANT > h
CONSONANT > k * 2
CONSONANT > l * 2
CONSONANT > m * 2
CONSONANT > n * 2
CONSONANT > p * 2
CONSONANT > r
CONSONANT > s * 2
CONSONANT > t * 2
CONSONANT > w
CONSONANT > z

VOWEL > a
VOWEL > e
VOWEL > i
VOWEL > o
VOWEL > u

# And that's it!
#
# Sample output:
# * Wohena Heli
# * Suke
# * Lo
# * Rahi
# * Lemo
# * Tofa
# * Nuba
# * Gudo
# * Pudi
# * Tuzi Pabe
